plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.smore'
version = '0.0.1-SNAPSHOT'
description = 'auction'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2025.0.0")
}

dependencies {
    // 데이터 저장소
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // 웹/API
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
//    TODO: STOMP 릴레이시 netty 가 필요한 이유 공부
    implementation 'io.projectreactor.netty:reactor-netty'

    // kafka
    implementation 'org.springframework.kafka:spring-kafka'

    // 클라우드/설정
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    // 개발 편의
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // 매핑
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    // Querydsl
    implementation 'io.github.openfeign.querydsl:querydsl-core:7.0'
    implementation 'io.github.openfeign.querydsl:querydsl-jpa:7.0'
    annotationProcessor 'io.github.openfeign.querydsl:querydsl-apt:7.0:jpa'

    // 런타임 드라이버
    runtimeOnly 'org.postgresql:postgresql'

    // 모니터링
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'

    // 테스트
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.instancio:instancio-junit:5.5.1'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'io.rest-assured:rest-assured:5.5.0'
    testImplementation 'io.rest-assured:json-path:5.5.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // 공통 모듈
    implementation project(":common")

    // MSK 설치
    implementation 'software.amazon.msk:aws-msk-iam-auth:2.2.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// Querydsl Q 클래스는 build/generated/querydsl 하위에 생성
def generated = layout.buildDirectory.dir("generated/querydsl")

tasks.named("compileJava") {
    // Annotation processor 출력(Q 클래스)을 generated 폴더로 보냄
    options.generatedSourceOutputDirectory.set(generated)
}

// 생성된 Querydsl 소스를 main 소스셋에 포함해 IDE/Gradle에서 Q 클래스 인식
sourceSets {
    main {
        java.srcDirs += generated
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
